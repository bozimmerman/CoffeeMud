# script for gargamel
ONCE_PROG
	QUESTCHAT I hate the smurfs! I hate them! I want them all dead right NOW!
	mpsetvar $i SMURFOCIDE  ''
~

SPEECH_PROG smurf hate kill all smurfocide quest dead want smurfs smurf? hate? kill? all? smurfocide? quest? dead? want? smurfs?
	say "$n" GIVE me 19 dead smurfs, including papa smurfs own corpse, and you will be rewarded.
endif
~

QUEST_TIME_PROG smurfocide 1
	QUESTCHAT My patience grows THIN! One more minute people!
~

QUEST_TIME_PROG smurfocide -1
	if var($i SMURFOCIDE != DONE)
		QUESTCHAT Ahh -- forget it! I have plans of my own...<evil grin>
	endif
	mpsetvar $i SMURFOCIDE ''
	mpsetvar * SMURFOCIDE ''
	mpsetvar $i PAPASMURF ''
	mpsetvar * PAPASMURF ''
~

GIVE_PROG 'the body of papa smurf'
	if QUESTWINNER($n smurfocide)
		say $n You've already committed smurfocide. Go away!
		MPPURGE $o
	else
		if var($i SMURFOCIDE == DONE)
			say "$n" Sorry, someone beat you!
			MPPURGE $o
		else
			if var($n SMURFOCIDE >= 19)
				mpsetvar $i SMURFOCIDE DONE
				say "$n" Wonderful!  Now I can make my famous smurf stew!
				MPFORCE Azreal emote licks his lips.
				MPPURGE $o
				mpexp $n 1000
				mpoload QuestPoint
				give "a quest point" "$n"
				mpquestwin $n smurfocide
				mpendquest smurfocide
			else
				say "$n" Well done.  This was a nasty one.  Thats $<$n SMURFOCIDE>. Now bring me the rest!
				mppurge $o
				MPSETVAR $n PAPASMURF DONE
			endif
		endif
	endif
~

GIVE_PROG 'the body of a smurf' 'the body of handy smurf' 'the body of grumpy smurf' 'the body of sleepy smurf' 'the body of brainy smurf' 'the body of hefty smurf' 'the body of jokey smurf' 'the body of smurfette'
	if QUESTWINNER($n smurfocide)
		say $n You've already committed smurfocide. Go away!
		MPPURGE $o
	else
		if var($i SMURFOCIDE == DONE)
			say "$n" Sorry, someone beat you!
			MPPURGE $o
		else
			mpsetvar $n SMURFOCIDE ++
			if var($n SMURFOCIDE >= 19) AND var($n PAPASMURF == DONE)
				mpsetvar $i SMURFOCIDE DONE
				say "$n" Wonderful!  Now I can make my famous smurf stew!
				MPFORCE Azreal emote licks his lips.
				mppurge $o
				mpexp $n 1000
				mpoload QuestPoint
				give "a quest point" "$n"
				mpquestwin $n smurfocide
				mpendquest smurfocide
			else
				say "$n" Lovely! Thats $<$n SMURFOCIDE>. Keep them coming!
				mppurge $o
			endif
		endif
	endif
~
