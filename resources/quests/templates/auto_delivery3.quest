#!QUESTMAKER_START_SCRIPT Auto Delivery #3
#The player is given a custom item 
#to deliver to one or more mobs you will create, somewhere else in your world.  A reward
#is optionally given upon delivery.
#
#!QUESTMAKER_PAGE Quest Name/Duration
#Enter the unique name and starting time/duration for your new quest.
#Times are entered as a simple number or math expression to denote a
#time in default ticks (4 second period), or you may follow the expression 
#with the word seconds, minutes, hours, days, mudhours, muddays, mudweeks,
#mudmonths, or mudyears.  Time expressions may include numbers, math 
#symbols, or the ? operator to generate random numbers.  Example:
#"5 + 3?10 minutes" would generate a frequency of 8-15 minutes.#
#
#Quest ID:
#Enter a unique name/id for your quest:
#$QUEST_ID=$UNIQUE_QUEST_NAME=auto_delivery_3
#
#Quest Name:
#Enter a friendly displayable name for your quest:
#$QUEST_NAME=$STRING=Auto Delivery #3
#
#Quest Frequency:
#This is the time between quest starts.
#$FREQUENCY=$TIMEEXPRESSION=90 minutes
#
#Quest Duration:
#This is how long your quest remains running, and how long a player 
#accepting the quest has to complete the quest and report back in.
#$DURATION=$TIMEEXPRESSION=90 minutes
#
#!QUESTMAKER_PAGE Specify the Quest Criteria and Instructions
#The criteria for players and instructions they need.
#
#Quest-Player Criterium:
#A Zapper Mask to describe what kinds of players will receive this quest.  
#You can use this to set race, level, or other requirements.
#$QUEST_CRITERIA=$ZAPPERMASK
#
#Instructions: 
#Specify some the instructions and justification for the quest.
#$QUEST_INSTRUCTIONSSTRING=($LONG_STRING)=Find ${2 *} at that one place and give him $[1 *].  Be careful!
#
#Success:
#This is what the player sees when the player completes the quest.
#$QUEST_WINSPEECH_TEXT=($LONG_STRING)=Delivery complete!
#
#!QUESTMAKER_PAGE Specify the Deliveree
#The Deliveree is the mob to which the item will be delivered. 
#
#Deliveree mobs: 
#Specify the names of the mob who will act as the deliveree mobs.  
#Use double-quotes around multi-word names.

#Specify below the name of the area that your deliveree mob or mobs will be created
#in and/or the rooms in which the mob or mobs will be created.  You may leave both blank
#to force the quest manager to create the mobs randomly throughout the world if you like.
#Use double-quotes around multi-word areas and room ids/names.  
#
#Deliveree Area:
#$DELIVEREE_AREA=($AREA)
#Deliveree Room(s):
#$DELIVEREE_ROOM=($ROOMID)
#
#Deliveree mobs: 
#Specify the mobs who will act as the deliveree mob or mobs.  You might want to mix things
#up by making them mobile.
#$DELIVEREE_MOBS=$MOBXML_ONEORMORE
#
#Captureables chance:
#Specify the percent chance that a mob will be found in a particular room.
#$DELIVEREE_CHANCE=($EXPRESSION)=10
#
#Deliveree response: 
#You may optionally specify some additional bit of speech the deliveree 
#mob will say to the players after they arrive.
#$DELIVEREE_ANNOUNCE=($LONG_STRING)=Are you here to deliver $[1 *]? If so, give it to me!
#
#!QUESTMAKER_PAGE Deliverable item
#Create the item which the player must take and give to the deliveree mob.  
#
#Deliverable Item: 
#You may select from the available items below or create a 
#new item just for this quest.
#$DELIVERABLE_ITEM=$ITEMXML
#
#Num Deliverees:
#specify the number of the deliverable items must be delivered to mobs
#matching the deliveree.  The quest will give the player the correct
#number of items to deliver.
#$DELIVEREE_COUNT=($EXPRESSION)=1
#
#Delivery response: 
#You may optionally specify some additional bit of speech the deliveree 
#mob will say to the players after they have handed over the item/items.
#$DELIVERY_RESPONSE=($LONG_STRING)=Thanks!
#
#!QUESTMAKER_PAGE Quest Completion
#Select some rewards for completing the deliveries:
#
#Reward items: 
#Specify zero or more items to choose from as a reward 
#$REWARD_ITEMS=$ITEMXML_ZEROORMORE
#
#Quest Point?
#$QUEST_POINT=$CHOOSE=YES,NO
#Amount of money (an amount, or leave blank):
#$GOLD=($EXPRESSION)
#Amount of experience points, blank for none, or a number% for percent of exp to next level:
#$EXP=($STRING)=10%
#
#Player Faction to give to or take from:
#$FACTION=($FACTION)
#If you selected a faction above, enter a new numeric amount, 
#or enter +value to add, or --value to subtract:
#$NUMFACTION=($STRING)
#
#Select whether a player may complete this quest multiple times,
#or 'prev' to require a previous quest, if multiple found.
#$MULTIPLE=$CHOOSE=YES,NO,PREV
#
#$CATEGORY=($HIDDEN)=
#
#!QUESTMAKER_END_SCRIPT Auto Delivery #3

set name $QUEST_ID
set display $QUEST_NAME
set author $#AUTHOR
set questtype auto_delivery3
set category $CATEGORY
set instructions $QUEST_INSTRUCTIONSSTRING

set wait $FREQUENCY
set interval 1
quiet

set room
set area
set pcmobgroup reselect any mask=$QUEST_CRITERIA
give script LOAD=$QUEST_ID_playertrack.script

set area
set room
set mobgroup
set mob
import items $QUEST_ID_item.xml
set itemgroup loadeditems
set item itemgroup

set room
set area $DELIVEREE_AREA
set roomgroup $DELIVEREE_ROOM

set duration $DURATION

<?xml version="1.0"?>
<FILE><NAME>$QUEST_ID_rewarditems.xml</NAME><DATA><ITEMS>$REWARD_ITEMS</ITEMS></DATA></FILE>
<FILE><NAME>$QUEST_ID_item.xml</NAME><DATA><ITEMS>$DELIVERABLE_ITEM</ITEMS></DATA></FILE>
<FILE><NAME>$QUEST_ID_mobs.xml</NAME><DATA><MOBS>$DELIVEREE_MOBS</MOBS></DATA></FILE>
<FILE><NAME>$QUEST_ID_playertrack.script</NAME>
<DATA>
ONCE_PROG 100
	mpsetvar $i INSTRUCTIONS $QUEST_INSTRUCTIONSSTRING
	if var($i $QUEST_ID_DELIVERED = '')
		mpsetvar $i $QUEST_ID_DELIVERED 0
	endif
	mpsetvar $i PROGRESS Delivered: $<$i $QUEST_ID_DELIVERED>/$DELIVEREE_COUNT
	IF QVAR(* DURATION != '0')
	    if QVAR(* REMAINING == '')
	        MPENDQUEST $i
	        RETURN
	    else
			mpsetvar $i TIME_REMAINING $%QVAR(* REMAINING)%
		endif
	ENDIF
	mpechoat $i $QUEST_INSTRUCTIONSSTRING
~

FUNCTION_PROG CHECK_DONE
	if VAR($i $QUEST_ID_DELIVERED >= $DELIVEREE_COUNT) and QUESTSCRIPTED($i *)
		if EVAL('$EXP' != '')
			mpexp $i $EXP
		endif
		if EVAL('$FACTION' != '')
			mpfaction $i $FACTION +$NUMFACTION
		endif
		if EVAL('$QUEST_POINT' == 'YES') AND !QUESTWINNER($n *)
			mpforce $i mpoload QuestPoint
			mpechoat $i You receive a quest point.
		endif
		if EVAL('$GOLD' != '') AND EVAL('$GOLD' > 0)
			mpforce $i mpoload $GOLD
			mpechoat $i You receive $b.
		endif
		mpforce $i mpoload fromfile $QUEST_ID_rewarditems.xml any
		if EVAL('$b' != '')
			mpechoat $i You receive $b.
		endif
		mpquestwin $i *
		mpecho $QUEST_WINSPEECH_TEXT
		mpendquest $i
		mpqset * STATISTICS SUCCESS
		IF EVAL('$MULTIPLE' == 'PREV')
			mptransfer $i $i
		ENDIF
	endif
~

FUNCTION_PROG CAN_ACCEPT
	if ISLIKE($n '$QUEST_CRITERIA') and ISPC($n) and ( EVAL('$MULTIPLE' != 'PREV') or QUESTWINNER($n previous) )
		if EVAL('$MULTIPLE' == 'YES') OR !QUESTWINNER($n *)
			if !QUESTSCRIPTED($n *)
				RETURN TRUE
			endif
		endif
	endif
	RETURN CANCEL
~

FUNCTION_PROG DO_ACCEPT
	mpscript $n INDIVIDUAL SAVABLE STATIC=LOAD=$QUEST_ID_playertrack.script
	mpqset * STATISTICS ACCEPTED
	for $0 = 0 to< $DELIVEREE_COUNT
		if !HAS($n '$[1 *]')
			mpat $n mpforce $n mpoload fromfile $QUEST_ID_item.xml '$[1 *]'
			MPQSET * QUESTOBJ $b
			mpat $n mpechoat $n You receive $[1 *].
		endif
	next
~

RAND_PROG 25
	IF QVAR(* DURATION != '0')
		if QVAR(* REMAINING == '')
			MPENDQUEST $i
		else
			mpsetvar $i TIME_REMAINING $%QVAR(* REMAINING)%
		endif
	ENDIF
~

QUEST_TIME_PROG * -1
	MPECHOAT $i The quest "$QUEST_NAME" has ended.
~

QUEST_TIME_PROG * 1
	MPECHOAT $i The quest "$QUEST_NAME" has 1 minute remaining.
~

GREET_PROG 100
	if QUESTMOB($n *) AND CANSEE($n $i) AND ISNPC($n)
		mpforce $n sayto $i $DELIVEREE_ANNOUNCE
	endif
~

FUNCTION_PROG DEL_LAST_MOB
	IF QUESTMOB('$<$i $QUEST_ID_LASTMOB>' *) AND ISNPC('$<$i $QUEST_ID_LASTMOB>')
		mppurge room '$<$i $QUEST_ID_LASTMOB>'
	ENDIF
~

CNCLMSG_PROG ENTER ALL
	if EVAL($n == $i)
		mpargset $g $%INROOM($t)%
		if EVAL('$<$i $QUEST_ID_LASTROOM>' != '') AND EVAL('$<$i $QUEST_ID_LASTMOB>' != '')
			mpat '$<$i $QUEST_ID_LASTROOM>' MPCALLFUNC DEL_LAST_MOB 
			mpsetvar $i $QUEST_ID_LASTMOB
			mpsetvar $i $QUEST_ID_LASTROOM
		endif
		if EVAL($g != '') AND VAR($i $QUEST_ID_DELIVERED < $DELIVEREE_COUNT)
			if QUESTROOM($g *)  or ( EVAL('$CAPTUREABLES_ROOMS' == 'ALL') AND QUESTAREA('$%INAREA($t)%' *))
				if !STRIN('$<$i $QUEST_ID_ROOMLIST>' $g)
					if !STRIN('$<$i $QUEST_ID_FOUNDROOMS>' $g)
						if rand($DELIVEREE_CHANCE) 
							mpsetvar $i $QUEST_ID_FOUNDROOMS $<$i $QUEST_ID_FOUNDROOMS> "$g"
						else
							mpsetvar $i $QUEST_ID_ROOMLIST $<$i $QUEST_ID_ROOMLIST> "$g"
						endif
					endif
					if STRIN('$<$i $QUEST_ID_FOUNDROOMS>' $g)
						mpat '$g' mpmload fromfile $QUEST_ID_mobs.xml any
						MPGSET $b REJUV 0
						MPQSET * QUESTOBJ $b
						mpsetvar $i $QUEST_ID_LASTMOB $b
						mpsetvar $i $QUEST_ID_LASTROOM $g
					endif
				endif
			endif
		endif
	endif
~

GIVING_PROG p '$[1 *]'
	if EVAL($o == '$[1 *]') AND QUESTMOB($t *) AND ISNPC($t)
		mpargset $g $%INROOM($t)%
		mpforce $t mpjunk $o
		mpforce $t sayto "$i" $DELIVERY_RESPONSE
		mpsetvar $i $QUEST_ID_DELIVERED ++
		mpsetvar $i $QUEST_ID_ROOMLIST $<$i $QUEST_ID_ROOMLIST> "$g"
		if EVAL($DELIVEREE_COUNT > 1)
			mpechoat $i That' $<$i $QUEST_ID_DELIVERED> / $DELIVEREE_COUNT
		endif
		mpsetvar $i PROGRESS Delivered: $<$i $QUEST_ID_DELIVERED>/$DELIVEREE_COUNT
		MPCALLFUNC CHECK_DONE
	else
		mpforce $t sayto "$i" What's this?
		mpforce $t give "$o" "$i"
	endif
~

ENTRY_PROG 100
	for $0 = 1 to '$%NUMMOBSROOM(*)%'
		mpargset $1 '$%ROOMMOB($0)%'
		if QUESTMOB($1 *) AND ISNPC($1)
			mpforce $1 sayto "$i" $DELIVEREE_ANNOUNCE
		endif
	next
~
</DATA></FILE>